! ICON
!
! ---------------------------------------------------------------
! Copyright (C) 2004-2026, DWD, MPI-M, DKRZ, KIT, ETH, MeteoSwiss
! Contact information: icon-model.org
!
! See AUTHORS.TXT for a list of authors
! See LICENSES/ for license information
! SPDX-License-Identifier: BSD-3-Clause
! ---------------------------------------------------------------

!------------------------------------------------------------------------------
!
! Description:
!
!   The coupling to radiation through the reff calculation and the
!   radar calculation make use of the two-moment formulations.
!   Hence, the number and mass moments are calculated from SBM and
!   are then passed to these subroutines. This introduces a spurious
!   dependency of SBM results on 2mom parameters (PSD, particle geometry).
!   A more consistent coupling that makes full use of the SBM particle
!   distributions and calculating reff directly from SBM is planned.

MODULE mo_sbm_driver

!==============================================================================
!
! Declarations:
!
! Modules used:
!------------------------------------------------------------------------------
! Microphysical constants and variables
!------------------------------------------------------------------------------

  USE mo_kind,                 ONLY: wp
  USE mo_exception,            ONLY: finish, message, message_text
  USE mo_2mom_mcrph_driver,    ONLY: two_moment_mcrph
  USE mo_sbm_main,             ONLY: fast_sbm
!==============================================================================

  IMPLICIT NONE
  PRIVATE

  CHARACTER(len=*), PARAMETER :: routine = 'mo_sbm_driver'
  INTEGER,          PARAMETER :: dbg_level = 25                   ! level for debug prints

  PUBLIC :: sbm

CONTAINS

  !==============================================================================
  !
  ! SBM warm phase microphysics
  !
  ! qnx in SBM is in units of 1/kg
  ! qx  in SBM is in units of kg/kg
  !
  !==============================================================================
  SUBROUTINE sbm(                         &
                       isize,             & ! in: array size
                       ke,                & ! in: end level/array size
                       is,                & ! in: start index, optional
                       ie,                & ! in: end index, optional
                       ks,                & ! in: start level ! needed for 2M
                       dt,                & ! in: time step
                       dz,                & ! in: vertical layer thickness
                       hhl,               & ! in: height of half levels
                       rho,               & ! in: density
                       pres,              & ! in: pressure
                       tke,               & ! in:  turbulent kinetic energy (on half levels, size nlev+1)
                       qv,                & ! inout: specific humidity (kg/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       qc, qnc,           & ! inout: cloud water (kg/kg, 1/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       qr, qnr,           & ! inout: rain (kg/kg, 1/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       qi, qni,           & ! inout: ice (kg/kg, 1/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       qs, qns,           & ! inout: snow (kg/kg, 1/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       qg, qng,           & ! inout: graupel (kg/kg, 1/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       qh, qnh,           & ! inout: hail (kg/kg, 1/kg atm_dyn_iconam/mo_nonhydro_state.f90)
                       ninact,            & ! inout: activated ice nuclei
                       tk,                & ! inout: temp
                       w,                 & ! in:    w
                       prec_r,            & ! inout: precip rate rain
                       prec_i,            & ! inout: precip rate ice
                       prec_s,            & ! inout: precip rate snow
                       prec_g,            & ! inout: precip rate graupel
                       prec_h,            & ! inout: precip rate hail
                       qrsflux,           & ! inout: 3D total precipitation rate
                       msg_level,         & ! in: msg_level
                       ithermo_water,     & ! in: thermodynamic option - needed for 2M
                       qbin,              &
                       qv_old,            &
                       temp_old,          &
                       exner,             & ! in: exner
                       lsbm_coupled)        ! FALSE: use 2M for feedback and run uncoupled SBM, TRUE: use SBM feedback

    ! Declare variables in argument list
    INTEGER,            INTENT (IN)  :: isize, ke    ! grid sizes
    INTEGER,  OPTIONAL, INTENT (IN)  :: is, ie, ks   ! start/end indices

    REAL(wp), INTENT (IN)            :: dt           ! time step

    ! Dynamical core variables
    REAL(wp), DIMENSION(:,:), INTENT(IN), TARGET :: dz, rho, pres, w, exner

    ! Optional Dynamical core variables
    REAL(wp), DIMENSION(:,:), INTENT(IN), POINTER :: tke

    REAL(wp), DIMENSION(:,:), INTENT(IN), TARGET :: hhl

    REAL(wp), DIMENSION(:,:), INTENT(INOUT), TARGET :: tk
    REAL(wp), DIMENSION(:,:), INTENT(IN), TARGET :: temp_old, qv_old
    ! Microphysics variables
    REAL(wp), DIMENSION(:,:), INTENT(INOUT) , TARGET :: &
         qv, qc, qnc, qr, qnr, qi, qni, qs, qns, qg, qng, qh, qnh, ninact
    REAL(wp), DIMENSION(:,:,:), INTENT(INOUT) , TARGET :: &
         qbin

    ! Precip rates, vertical profiles
    REAL(wp), DIMENSION(:), INTENT (INOUT) :: &
         &               prec_r, prec_i, prec_s, prec_g, prec_h
    REAL(wp), DIMENSION(:,:), INTENT (INOUT) :: qrsflux
    INTEGER,  INTENT (IN)             :: msg_level
    LOGICAL,  OPTIONAL, INTENT (IN)   :: lsbm_coupled
    INTEGER,  OPTIONAL,  INTENT (IN)  :: ithermo_water

    REAL(wp), DIMENSION(isize,ke) ::        &
         &  theta,         & ! potential temperature
         &  lh_rate,       &
         &  ce_rate,       &
         &  cldnucl_rate,  &
         &  nccn2,         &
         &  diag_satur_ba,diag_satur_aa,diag_satur_am,diag_supsat_out, &
         &  reff,reffc,reffr, &
         &  qv_sbm,qc_sbm,qr_sbm,qi_sbm,qs_sbm,qg_sbm,qnc_sbm,qnr_sbm,qni_sbm,qns_sbm,qng_sbm
    REAL(wp), DIMENSION(isize) :: prec_r_sbm,prec_s_sbm,prec_g_sbm

    INTEGER  :: its,ite,kts,kte
    INTEGER  :: ii,kk

    ! start/end indices
    IF (PRESENT(is)) THEN
      its = is
    ELSE
      its = 1
    END IF
    IF (PRESENT(ie)) THEN
      ite = ie
    ELSE
      ite = isize
    END IF

    kts = 1
    kte = ke

    DO kk = kts, kte
      DO ii = its, ite
        nccn2(ii,kk) = 0.0_wp
        lh_rate(ii,kk) = 0.0_wp
        ce_rate(ii,kk) = 0.0_wp
        cldnucl_rate(ii,kk) = 0.0_wp

        qv_sbm(ii,kk)=qv(ii,kk)
        theta(ii,kk) = tk(ii,kk)/exner(ii,kk) !just initialisation, we can put zero

        qc_sbm(ii,kk) = 0.0_wp
        qr_sbm(ii,kk) = 0.0_wp
        qi_sbm(ii,kk) = 0.0_wp
        qs_sbm(ii,kk) = 0.0_wp
        qg_sbm(ii,kk) = 0.0_wp
        qnc_sbm(ii,kk) = 0.0_wp
        qnr_sbm(ii,kk) = 0.0_wp
        qni_sbm(ii,kk) = 0.0_wp
        qns_sbm(ii,kk) = 0.0_wp
        qng_sbm(ii,kk) = 0.0_wp

        diag_satur_ba(ii,kk)=0.0_wp
        diag_satur_aa(ii,kk)=0.0_wp
        diag_satur_am(ii,kk)=0.0_wp
        diag_supsat_out(ii,kk)=0.0_wp
      END DO
    END DO
    DO ii = its, ite
      prec_r_sbm(ii) = 0.0_wp
      prec_s_sbm(ii) = 0.0_wp
      prec_g_sbm(ii) = 0.0_wp
    END DO

    CALL FAST_SBM(dt=dt                   &!in:    dt
                 ,dz8w=dz                 &!in:    vertical layer thickness
                 ,rho_phy=rho             &!in:    density
                 ,p_phy=pres              &!in:    pressure
                 ,pi_phy=exner            &!in:    exner
                 ,w=w                     &!in:    velocities
                 ,qv_old=qv_old           &
                 ,th_phy=theta            &!inout: theta. Check how to update prognostic theta_v
                 ,qv=qv_sbm               &
                 ,chem_new=qbin           &!inout: 99 mass bins
                 ,prec_r_sbm=prec_r_sbm   &!inout: 1 time step precipitation (mm/sec).
                 ,qc=qc_sbm               &!inout: cloud water: input: 0
                 ,qr=qr_sbm               &!inout: rain water:  input: 0
                 ,qnc=qnc_sbm             &!inout: cloud water concentration:input: 0
                 ,qnr=qnr_sbm             &!inout: rain water concentration: input: 0
                 ,qna=nccn2               &!inout: ccn concentration:   input: 0
                 ,lh_rate=lh_rate         &!inout: rate 1:      input: 0, output can go further to the model
                 ,ce_rate=ce_rate         &!inout: rate 2:      input: 0, output can go further to the model
                 ,cldnucl_rate=cldnucl_rate &!inout: rate 3:    input: 0, output can go further to the model
                 ,its=its,ite=ite, kts=kts,kte=kte &!in:    subdomain indeces
                 ,diag_satur_ba=diag_satur_ba          & !inout: diagnostic supersaturation before advection
                 ,diag_satur_aa=diag_satur_aa          & !inout: diagnostic supersaturation after advection
                 ,diag_satur_am=diag_satur_am          & !inout: diagnostic supersaturation after microphysics
                 ,diag_supsat_out=diag_supsat_out      & !inout: diagnostic supersaturation after cond_evap subroutines
                 ,temp_old=temp_old       &
                 ,temp_new=tk             &
                 ,reff=reff               &
                 ,reffc=reffc             &
                 ,reffr=reffr             &
                 ,qi=qi_sbm               &
                 ,qs=qs_sbm               &
                 ,qg=qg_sbm               &
                 ,qni=qni_sbm             &
                 ,qns=qns_sbm             &
                 ,qng=qng_sbm             &
                 ,prec_s_sbm=prec_s_sbm   &
                 ,prec_g_sbm=prec_g_sbm)

    IF (lsbm_coupled) then ! use SBM feedback
      DO kk = kts, kte
        DO ii = its, ite
          qv(ii,kk)=qv_sbm(ii,kk)     !kg/kg
          qc(ii,kk)=qc_sbm(ii,kk)     !kg/kg
          qr(ii,kk)=qr_sbm(ii,kk)     !kg/kg
          qnc(ii,kk)=qnc_sbm(ii,kk)   !1/kg
          qnr(ii,kk)=qnr_sbm(ii,kk)   !1/kg
          tk(ii,kk)=theta(ii,kk)*exner(ii,kk)
          qi(ii,kk)=qi_sbm(ii,kk)     !kg/kg
          qni(ii,kk)=qni_sbm(ii,kk)   !1/kg
          qs(ii,kk)=qs_sbm(ii,kk)     !kg/kg
          qns(ii,kk)=qns_sbm(ii,kk)   !1/kg
          qg(ii,kk)=qg_sbm(ii,kk)     !kg/kg
          qng(ii,kk)=qng_sbm(ii,kk)   !1/kg
          qh(ii,kk)=0.0_wp  ! inout: hail ok
          qnh(ii,kk)=0.0_wp ! inout: hail ok
        END DO
      END DO
      DO ii = its, ite
        prec_r(ii)=prec_r_sbm(ii)
        prec_i(ii)=0.0_wp
        prec_s(ii)=prec_s_sbm(ii)
        prec_g(ii)=prec_g_sbm(ii)
        prec_h(ii)=0.0_wp
      END DO
    ELSE ! 2M-->dynamics, sbm-->output only
      CALL two_moment_mcrph(           &
                       isize  = isize, &!nproma,             &!in: array size
                       ke     = ke, &!nlev,                  &!in: end level/array size
                       is     = is, &!i_startidx,            &!in: start index
                       ie     = ie, &!i_endidx,              &!in: end index
                       ks     = ks, &!kstart_moist(jg),      &!in: start level
                       dt     = dt, &!tcall_gscp_jg ,        &!in: time step
                       dz     = dz, &!p_metrics%ddqz_z_full(:,:,jb),  &!in: vertical layer thickness
                       hhl    = hhl, &!p_metrics%z_ifc(:,:,jb),        &!in: height of half levels
                       rho    = rho, &!p_prog%rho(:,:,jb  )       ,    &!in:  density
                       pres   = pres, &!p_diag%pres(:,:,jb  )      ,    &!in:  pressure
                       tke    = tke, &!ptr_tke_loc, &!in:  turbulent kinetic energy (on half levels, size nlev+1)
                       qv     = qv, &!ptr_tracer (:,:,jb,iqv), &!inout:sp humidity
                       qc     = qc, &!ptr_tracer (:,:,jb,iqc), &!inout:cloud water
                       qnc    = qnc, &!ptr_tracer (:,:,jb,iqnc),&!inout: cloud droplet number
                       qr     = qr, &!ptr_tracer (:,:,jb,iqr), &!inout:rain
                       qnr    = qnr, &!ptr_tracer (:,:,jb,iqnr),&!inout:rain droplet number
                       qi     = qi, &!ptr_tracer (:,:,jb,iqi), &!inout: ice
                       qni    = qni, &!ptr_tracer (:,:,jb,iqni),&!inout: cloud ice number
                       qs     = qs, &!ptr_tracer (:,:,jb,iqs), &!inout: snow
                       qns    = qns, &!ptr_tracer (:,:,jb,iqns),&!inout: snow number
                       qg     = qg, &!ptr_tracer (:,:,jb,iqg), &!inout: graupel
                       qng    = qng, &!ptr_tracer (:,:,jb,iqng),&!inout: graupel number
                       qh     = qh, &!ptr_tracer (:,:,jb,iqh), &!inout: hail
                       qnh    = qnh, &!ptr_tracer (:,:,jb,iqnh),&!inout: hail number
                       ninact = ninact, &!ptr_tracer (:,:,jb,ininact), &!inout: IN number
                       tk     = tk, &!p_diag%temp(:,:,jb),            &!inout: temp
                       w      = w, &!p_prog%w(:,:,jb),               &!inout: w
                       prec_r = prec_r, &!prm_diag%rain_gsp_rate (:,jb),  &!inout precp rate rain
                       prec_i = prec_i, &!prm_diag%ice_gsp_rate (:,jb),   &!inout precp rate ice
                       prec_s = prec_s, &!prm_diag%snow_gsp_rate (:,jb),  &!inout precp rate snow
                       prec_g = prec_g, &!prm_diag%graupel_gsp_rate (:,jb),&!inout precp rate graupel
                       prec_h = prec_h, &!prm_diag%hail_gsp_rate (:,jb),  &!inout precp rate hail
                       qrsflux= qrsflux, &!prm_diag%qrs_flux(:,:,jb),      & !inout: 3D precipitation flux for LHN
                       msg_level = msg_level,                   &
                       & l_cv=.TRUE.,                           &
                       & ithermo_water=ithermo_water) !atm_phy_nwp_config(jg)%ithermo_water ) !< in: latent heat choice

    END IF

  END SUBROUTINE sbm

END MODULE mo_sbm_driver
